#!/usr/bin/env zsh

export DOT_PROMPT_HEIGHT=3

function _fill_line() {
    emulate -L zsh

    local left_len=$(_prompt_length $1)
    local right_len=$(_prompt_length $2 9999)
    local pad_len=$((COLUMNS - left_len - right_len - ${ZLE_RPROMPT_INDENT:-1}))

    if (( pad_len < 1 )); then
        # Not enough space for the right part. Drop it.
        echo -E - ${1}
    else
        local pad=${(pl.$pad_len..═.)}  # pad_len spaces
        echo -E - ${1}${pad}${2}
    fi
}

function _prompt_length() {
    emulate -L zsh

    local COLUMNS=${2:-$COLUMNS}
    local -i x y=$#1 m

    if (( y )); then
        while (( ${${(%):-$1%$y(l.1.0)}[-1]} )); do
            x=y
            (( y *= 2 ));
        done

        local xy
        while (( y > x + 1 )); do
            m=$(( x + (y - x) / 2 ))
            typeset ${${(%):-$1%$m(l.x.y)}[-1]}=$m
        done
    fi
    echo $x
}

function __position_prompt_bottom {
    local t_lines="$(echoti lines)"
    [[ $# == 0 ]] && margin=1 || margin=$((${1}))
    echoti cup $(( (${t_lines}-${DOT_PROMPT_HEIGHT}) + $((${margin})) )) 0
}

function __clear_prompt {
    __position_prompt_bottom 2
    repeat ${DOT_PROMPT_HEIGHT} { echoti cuu1 && echoti dl1 }
}

clear-screen() { echoti clear; __position_prompt_bottom 2; }
clear-screen-zle() { echoti clear; __position_prompt_bottom 2; zle redisplay; }

function _php_version {
    PHP=$(which php)
    PHP_VERSION="$($PHP -r 'echo PHP_VERSION;')"

    echo " ${PHP_VERSION[1,3]}"
}


function set-prompt {
    emulate -L zsh

    local mfill_l mfill_r bpad_l bpad_r mid_fill top_left

    mfill_r=' ╔═'; mfill_l='═╗ '; bpad_r=' ╚══'; bpad_l='══╝ '

    mid_fill="$(_fill_line "$mfill_r" "$mfill_l")"
    top_left='%F{012}   ${${SHELL/#\/bin\//}:u} | $TERM_PROGRAM | $TERM %f '

    PS1=$'\n'$top_left$'\n'${mid_fill}$'\n'$bpad_r' ${PWD/#$HOME/ } '
    RPROMPT=' $(_php_version) ══╝ '

}

setopt PROMPT_SUBST
autoload -Uz add-zsh-hook

add-zsh-hook precmd set-prompt
add-zsh-hook preexec __clear_prompt
zle -N clear-screen-zle

function TRAPWINCH () {
    clear-screen-zle
    set-prompt
    zle && zle reset-prompt

    clear-screen-zle
}
