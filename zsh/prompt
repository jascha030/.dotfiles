#!/usr/bin/env zsh

export DOT_PROMPT_HEIGHT=3

function prompt-length() {
    emulate -L zsh
    local -i COLUMNS=${2:-COLUMNS}
    local -i x y=${#1} m
    if (( y )); then
        while (( ${${(%):-$1%$y(l.1.0)}[-1]} )); do
            x=y
            (( y *= 2 ))
        done
        while (( y > x + 1 )); do
            (( m = x + (y - x) / 2 ))
            (( ${${(%):-$1%$m(l.x.y)}[-1]} = m ))
        done
    fi
    typeset -g REPLY=$x
}

function fill_line {
    emulate -L zsh

    prompt-length $1
    local -i left_len=REPLY
    prompt-length $2 9999

    local -i right_len=REPLY
    local -i pad_len=$((COLUMNS - left_len - right_len - ${ZLE_RPROMPT_INDENT:-1}))

    if (( pad_len < 1 )); then
        # Not enough space for the right part. Drop it.
        typeset -g REPLY=$1
    else
        local pad=${(pl.$pad_len..═.)}  # pad_len spaces
        typeset -g REPLY=${1}${pad}${2}
    fi
}


function __position_prompt_bottom {
    local -i t_lines=${LINES}
    [[ $# == 0 ]] && margin=1 || margin=$((${1}))
    echoti cup $(( (${t_lines}-${DOT_PROMPT_HEIGHT}) + $((${margin})) )) 0
}

function __clear_prompt {
    __position_prompt_bottom 2
    repeat ${DOT_PROMPT_HEIGHT} { echoti cuu1 && echoti dl1 }
}

clear-screen() { echoti clear; __position_prompt_bottom 2; }
clear-screen-zle() { echoti clear; __position_prompt_bottom 2; zle redisplay; }

function _php_version {
    emulate -L zsh

    local _php=$(which php)

    PHP_VERSION="$($_php -r 'echo PHP_VERSION;')"
    local w="%F{013} ${PHP_VERSION[1,3]}%f"
    
    typeset -g WPHP=$w
}


function set-prompt {
    emulate -L zsh

    local REPLY 
    local WPHP
    local mpad_r=' ╔═'; 
    local mpad_l='═╗ '; 
    local bpad_l=' ╚══'; 
    local bpad_r='══╝ '

    _php_version "$bpad_r "
    local top_left="%F{012}   ${${SHELL/#\/bin\//}:u} | ${TERM_PROGRAM} | ${TERM} %f"
    local bottom_left="${bpad_l} ${PWD/#$HOME/ } "
    local bottom_right="${WPHP} ${bpad_r}"

    fill_line "$mpad_r" "$mpad_l"
    PROMPT=$'\n'$top_left$'\n'$REPLY$'\n'$bottom_left
    RPROMPT=$bottom_right
}

# setopt PROMPT_SUBST
autoload -Uz add-zsh-hook

add-zsh-hook precmd set-prompt
add-zsh-hook preexec __clear_prompt
zle -N clear-screen-zle

function TRAPWINCH () {
    zle && clear-screen-zle
    set-prompt
    zle && zle reset-prompt

    clear-screen-zle
}

