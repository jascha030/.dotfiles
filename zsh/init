#!/usr/bin/env zsh

#######################################################################################################################
# By Jascha030                                  Dotfiles Initialization         https://github.io/jascha030/.dotfiles #
#######################################################################################################################

if ! (( ${+XDG_CONFIG} )); then 
    export XDG_CONFIG=${HOME}/.config
fi

# antigen.zsh path. based on simple install, overwrite if installed through brew.
if ! (( ${+DOT_ANTIGEN_PATH} )); then 
    export DOT_ANTIGEN_PATH=${HOME}/antigen.zsh
fi

# .antigenrc path.
if ! (( ${+DOT_ANTIGEN_RC_PATH} )); then 
    export DOT_ANTIGEN_RC_PATH=${HOME}/.antigenrc
fi

# Alternative dir for histfiles
if ! (( ${+DOT_DATA_DIR} )); then 
    export DOT_DATA_DIR=${XDG_CONFIG}/datafiles
fi

# Prompt height in lines
if ! (( ${DOT_PROMPT_HEIGHT} )); then 
    export DOT_PROMPT_HEIGHT=2
fi

#--------------------------------------------- Internally used functions ---------------------------------------------#

function __fpath_add {
    fpath=( "$@" ${fpath[@]} )
}


function __autoload {
    local a
    for a in "$@"; do
        [[ -d "$a" ]] && autoload -Uz ${a}/*(.:t)
    done
}

function __df_load_antigen {
    # Load or install antigen
    [[ ! -f "$1" ]] && curl -L git.io/antigen > "$1"
    # Sourze antigen.zsh 
    source "$1"
    # Load rc
    [[ -f "$2" ]] && antigen init "$2"
}

#--------------------------------------------------------- Init ------------------------------------------------------#

if (( ${+DOT_COMP_DIRS} )); then 
    __fpath_add "${DOT_COMP_DIRS[@]}"
fi 

__fpath_add "${DOT_ZSH}/zfunc" "${DOT_ZSH}/completions"  
__autoload "${DOT_ZSH}/zfunc"

# Source files that are required early.
if (( ${+DOT_SOURCES} )); then 
    safe_source "${DOT_SOURCES[@]}"
fi

if (( ${+DOT_BASH_COMPLETIONS_DIR} )); then 
    __fpath_add "${DOT_BASH_COMPLETIONS_DIRS[@]}"
    autoload -U +X bashcompinit && bashcompinit
    # safe_source ${DOT_BASH_COMPLETIONS_DIR}/*.sh
fi

# Utility function, to merge arrays...
function merge_arrays {
    # undefined 
    builtin autoload -X
}

# Run and time zsh initialization 10 times.
function speedtest {
    # undefined
    builtin autoload -X
}

# Assure required dirs and files exist.
assert_dirs "${DOT_DATA_DIR}"
assert_files "${DOT_DATA_DIR}/.zsh_history" "${DOT_DATA_DIR}/.mysql_history"

if (( ${+DOT_REQUIRED_DIRS} )); then 
    assert_dirs "${DOT_REQUIRED_DIRS[@]}"  
fi

if (( ${+DOT_REQUIRED_FILES} )); then 
    assert_files "${DOT_REQUIRED_FILES[@]}"
fi


# Load antigen plugins.
__df_load_antigen "${DOT_ANTIGEN_PATH}" "${DOT_ANTIGEN_RC_PATH}"

function __clear_prompt {
    repeat ${DOT_PROMPT_HEIGHT} { echoti cuu1 && echoti dl1 }
}

# Add preexec hook.
autoload -Uz add-zsh-hook
add-zsh-hook preexec __clear_prompt 

# Todo: figure out how to make this more dynamic.
eval "$(starship init zsh)"
eval "$(zoxide init zsh)"
eval "$(teleport-dir init)"
eval "$(fnm env --use-on-cd)"


# Source late files like aliases etc.
if (( ${+DOT_AFTER_INIT_SOURCES} )); then 
    safe_source "${DOT_AFTER_INIT_SOURCES[@]}"
fi

# Allow to turn off intro-msg for debugging.
if (( ${+DOT_DISABLE_MSG} )); then 
    unset DOT_DISABLE_MSG 
else 
    (( ${+PROFILE_ZSHRC} )) && export DOT_LOL_OUTPUT_ONLY=1

    lolmsg
    
    (( ${+PROFILE_ZSHRC} )) && unset DOT_LOL_OUTPUT_ONLY;
fi
