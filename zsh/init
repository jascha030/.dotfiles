#!/usr/bin/env zsh

#######################################################################################################################
#                                               Dotfiles Initialization                                               #
# By Jascha030                                                                  https://github.io/jascha030/.dotfiles #
#######################################################################################################################


# Dotfiles zsh function root dir.
export DOT_ZFUNC="${DOT_ZSH}/zfunc"

# Default antigen.zsh path.
# Based on a normal install, overwrite if installed through brew.
if ! (( ${+DOT_ANTIGEN_PATH} )); then 
    export DOT_ANTIGEN_PATH="${HOME}/antigen.zsh"
fi

# Left rc path open to edit, allthough Antigen acts weird when rc not @ $HOME.
if ! (( ${+DOT_ANTIGEN_RC_PATH} )); then 
    export DOT_ANTIGEN_RC_PATH="${HOME}/.antigenrc"
fi


# Internally used functions.

# Autoload all files in dir.
function __autoload_dirs {
    local a
 
    for a in "$@"; do
        [[ -f "$a" ]] && autoload -Uz "$a"

        [[ -d "$a" ]] && autoload -Uz ${a}/*(.:t)
    done
}

# Load antigen plugins.
function __df_load_antigen {
    # Todo: check
    if [[ ! -f "$DOT_ANTIGEN_PATH" ]]; then 
        curl -L git.io/antigen > $DOT_ANTIGEN_PATH
    fi
  
    source "$DOT_ANTIGEN_PATH"

    if [[ -f "$DOT_ANTIGEN_RC_PATH" ]]; then 
        antigen init "$DOT_ANTIGEN_RC_PATH"
    fi
}


# Source files and autoload functions that are required early.
# Default directory for functions.
# Add by exporting (array) `DOT_AUTOLOAD_DIRS`.
local AUTOLOAD_DIRS=( "${DOT_ZFUNC}" )
(( ${+DOT_AUTOLOAD_DIRS} )) && $AUTOLOAD_DIRS=( "${AUTOLOAD_DIRS}" "${DOT_AUTOLOAD_DIRS[@]}" )

# Default directory for completions.
# Add by exporting (array) `DOT_COMP_DIRS`.
local COMP_DIRS=( "${DOT_ZSH}/completions" )
(( ${+DOT_COMP_DIRS} )) && COMP_DIRS=( "${COMP_DIRS[@]}" "${DOT_COMP_DIRS[@]}" )


fpath=( "${fpath[@]}" "${AUTOLOAD_DIRS[@]}" "${COMP_DIRS[@]}" )
__autoload_dirs "${AUTOLOAD_DIRS[@]}"

# Path
safe_source "${DOT_ZSH}/path"
typeset -aU path

# Source files that are required early.
if (( ${+DOT_SOURCES} )); then 
    safe_source "${DOT_SOURCES[@]}"
fi


# Lazy load functions that are only used conditionally.
# All functions reside in the default functions dir.

# Checks if directories or creates them using `mkdir -p`.
function assert_dirs {
    # undefined 
    builtin autoload -X
}

# Checks if files exist or creates them using `touch`.
function assert_files {
    # undefined 
    builtin autoload -X
}

# Display msg after load.
function lolmsg {
    # undefined
    builtin autoload -X
}

# Utility function, to merge arrays...
function merge_arrays {
    # undefined 
    builtin autoload -X
}

# Run and time zsh initialization 10 times.
function z_speedtest {
    # undefined
    builtin autoload -X
}

function source_dirs {
    # undefined 
    builtin autoload -X
}


# Assure required dirs exist.
(( ${+DOT_REQUIRED_DIRS} )) && assert_dirs "${DOT_REQUIRED_DIRS[@]}"  

# Assure required files exist.
(( ${+DOT_REQUIRED_FILES} )) && assert_files "${DOT_REQUIRED_FILES[@]}"

# Load antigen plugins.
__df_load_antigen

# Todo: figure out how to make this more dynamic.
eval "$(starship init zsh)"
eval "$(zoxide init zsh)"
eval "$(teleport-dir init)"
eval "$(fnm env)"


# Source other zsh dotfiles like aliases etc.
if (( ${+DOT_AFTER_INIT_SOURCES} )); then 
    safe_source "${DOT_AFTER_INIT_SOURCES[@]}"
fi


# Allow to turn off lolmsg when debugging.
if ! (( ${+DOT_DISABLE_MSG} )); then 
    lolmsg "${VIM_TERM_MODE_ACTIVE}" 
else 
  unset DOT_DISABLE_MSG
fi

