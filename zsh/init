#!/usr/bin/env zsh

#######################################################################################################################
# By Jascha030                                  Dotfiles Initialization         https://github.io/jascha030/.dotfiles #
#######################################################################################################################

# Alternative dir for histfiles
if ! (( ${+DOT_DATA_DIR} )); then 
    export DOT_DATA_DIR=${XDG_CONFIG_HOME}/datafiles
fi

# Prompt height in lines
! (( ${DOT_PROMPT_HEIGHT} )) && export DOT_PROMPT_HEIGHT=2

# Decide which msg to display.
(( ${+VIM} && ${+VIMRUNTIME} && ${+MYVIMRC} )) && export _IS_VIMTERM=true || export _IS_VIMTERM=false
[[ "$_IS_VIMTERM" == true ]] && LOL_MSG="${DOT_NEOVIM_LOL_MSG}" || LOL_MSG="${DOT_DEFAULT_LOL_MSG}"

#--------------------------------------------- Internally used functions ---------------------------------------------#

function __fpath_add {
    fpath=( "$@" ${fpath[@]} )
}

function __path_add {
    path=( "$@" ${path[@]} )
}

function __autoload {
    local a
    for a in "$@"; do
        [[ -d "$a" ]] && autoload -Uz ${a}/*(.:t)
    done
}

function zsh_add_plugin {
    PLUGIN_NAME=$(echo $1 | cut -d "/" -f 2)

    if [ -d "$ZDOTDIR/plugins/$PLUGIN_NAME" ]; then 
        _PLUGIN_DIR="$ZDOTDIR/plugins/$PLUGIN_NAME" 
        [ -f "$_PLUGIN_DIR/$PLUGIN_NAME.plugin.zsh" ] && source "$_PLUGIN_DIR/$PLUGIN_NAME.plugin.zsh" \
        || source "$_PLUGIN_DIR/$PLUGIN_NAME.zsh"
    else
        git clone "https://github.com/$1.git" "$ZDOTDIR/plugins/$PLUGIN_NAME"
    fi
}

#--------------------------------------------------------- Init ------------------------------------------------------#

if (( ${+DOT_COMP_DIRS} )); then 
    __fpath_add "${DOT_COMP_DIRS[@]}"
fi 

__fpath_add "${DOTFILES}/zsh/zfunc" "${DOTFILES}/zsh/completions"  
__autoload "${DOTFILES}/zsh/zfunc"

# Source files that are required early.
if (( ${+DOT_SOURCES} )); then 
    safe_source "${DOT_SOURCES[@]}"
fi

# Utility function, to merge arrays...
function merge_arrays {
    # undefined 
    builtin autoload -X
}

# Run and time zsh initialization 10 times.
function speedtest {
    # undefined
    builtin autoload -X
}

function echoerr {
    # undefined
    builtin autoload -X
}

function sb2init {
    # undefined
    builtin autoload -X
}

# Assure required dirs and files exist.
assert_dirs "${DOT_DATA_DIR}"
assert_files "${DOT_DATA_DIR}/.zsh_history" "${DOT_DATA_DIR}/.mysql_history"

if (( ${+DOT_REQUIRED_DIRS} )); then 
    assert_dirs "${DOT_REQUIRED_DIRS[@]}"  
fi

if (( ${+DOT_REQUIRED_FILES} )); then 
    assert_files "${DOT_REQUIRED_FILES[@]}"
fi

safe_source "${ZDOTDIR}/plugin-spec"

# TODO: figure out how to make this more dynamic.
eval "$(starship init zsh)"
eval "$(zoxide init zsh)"
eval "$(teleport-dir init)"
eval "$(fnm env --use-on-cd)"

export LS_COLORS="$(vivid generate jassie030)"

# Source late files like aliases etc.
if (( ${+DOT_AFTER_INIT_SOURCES} )); then 
    safe_source "${DOT_AFTER_INIT_SOURCES[@]}"
fi
