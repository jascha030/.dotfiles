#!/usr/bin/env zsh

# Overrides from env.
(( ${+CAT_OVERRIDE} )) && alias cat=${CAT_OVERRIDE}
(( ${+LS_OVERRIDE} ))  && alias ls=${LS_OVERRIDE}

# Open cwd as project in various JetBrains IDE's
(( ${+commands[idea]} ))     && alias idea='idea .'
(( ${+commands[phpstorm]} )) && alias pstorm='phpstorm .'
(( ${+commands[webstorm]} )) && alias wstorm='webstorm .'
(( ${+commands[egrep]} ))    && alias egrep='egrep --color=auto --exclude-dir={.bzr,CVS,.git,.hg,.svn,.idea,.tox}'
(( ${+commands[fgrep]} ))    && alias fgrep='fgrep --color=auto --exclude-dir={.bzr,CVS,.git,.hg,.svn,.idea,.tox}'

function ll() {
    local ls_cmd='ls'
    local ls_args='-Ahl'

    if (( ${+LS_OVERRIDE} )); then
        ls_cmd=$LS_OVERRIDE
    fi

    if (( ${+LS_DEFAULT_ARGS} )); then
        ls_args=$LS_DEFAULT_ARGS
    fi

    eval "$ls_cmd" "$ls_args";
}

# Copy pwd to clipboard
if (( ! ${+commands[cpd]} )); then
    function cpd { builtin echo -en "${PWD}" | pbcopy; }

    autoload -Uz cpd
fi

# Copy file contents to clipboard
if (( ! ${+commands[cpc]} )); then
    function cpc { cat "$@" | pbcopy; }

    autoload -Uz cpc
fi

# Isolated php and composer
if (( ${+commands[valet]} )); then
    function __iphp {
        if [[ -f "${PWD}/composer.json" ]]; then
            ${HOME}/.composer/vendor/bin/valet php "$@"
        else
            export PHP_VERSION="${$(/opt/homebrew/bin/php -r 'echo PHP_VERSION;')[1,3]}"

            /opt/homebrew/bin/php "$@"
        fi
    }

    function __icomposer {
        if [[ -f "${PWD}/composer.json" ]]; then
            ${HOME}/.composer/vendor/bin/valet composer "$@"
        else
            if [[ -f "${PWD}/app/public/composer.json" ]]; then
                ${HOME}/.composer/vendor/bin/valet composer --working-dir="${PWD}/app/public" "$@"
            else
                /Users/jaschavanaalst/tools/composer "$@"
            fi
        fi
    }

    autoload -Uz __iphp __icomposer
    alias php='__iphp'
    alias composer='__icomposer'
fi

if (( ${+LAZYGIT_CONFIG} )); then
    function __lazygit {
        if [[ "$(is_dark)" == 0 ]] && [[ "$(is_neovim)" == 0 ]]; then
            local config=${LAZYGIT_CONFIG}/config_light.yml
        else
            local config=${LAZYGIT_CONFIG}/config_dark.yml
        fi

        lazygit -ucf "$config"
    }

    autoload -Uz __lazygit
    alias lazygit='__lazygit'
fi
