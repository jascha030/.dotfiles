#!/usr/bin/env zsh

# -------------------------------------------------------- FZF --------------------------------------------------------

_FZF_BASE_OPTS="--info=inline --padding=1 --margin=2"

__FZF_DARK="${_FZF_BASE_OPTS} \
--color=fg:#c8d3f5,hl:#5f5fec \
--color=fg+:#fffcfc,bg+:#32466e,hl+:#0083f7 \
--color=info:#6183bb,prompt:#ea1479,pointer:#ffcc00 \
--color=marker:#ea1479,spinner:#5f5fec,header:#465182,border:#c8d3f5"

__FZF_LIGHT="${_FZF_BASE_OPTS} \
--color=fg:#444a73,hl:#5f5fec \
--color=fg+:#fffcfc,bg+:#c8d3f5,hl+:#0083f7 \
--color=info:#6183bb,prompt:#ea1479,pointer:#ffcc00 \
--color=marker:#ea1479,spinner:#5f5fec,header:#0083f7,border:#444a73"

# Update theme dynamically based on macOS appearance
__fzf_update_theme() {
    if is_dark; then
        FZF_DEFAULT_OPTS="$__FZF_DARK"
    else
        FZF_DEFAULT_OPTS="$__FZF_LIGHT"
    fi

    export FZF_DEFAULT_OPTS
    export FZF_COMPLETION_OPTS="$FZF_DEFAULT_OPTS"
}

if (( ! ${path[(Ie)/opt/homebrew/opt/fzf/bin]} )); then
    path+=/opt/homebrew/opt/fzf/bin
fi

# Load completion only in interactive shells
if [[ $- == *i* ]]; then
    source "/opt/homebrew/opt/fzf/shell/completion.zsh" 2> /dev/null
fi

# Lazy-load FZF keybindings -------------------------------------------------------------------------------------------

__fzf_keybindings_loaded=0

__load_fzf_keybindings() {
    (( __fzf_keybindings_loaded )) && return
    __fzf_keybindings_loaded=1
    source "/opt/homebrew/opt/fzf/shell/key-bindings.zsh"
}

# Main wrapper: lazy loads, updates theme, then calls real fzf
fzf() {
    __load_fzf_keybindings
    __fzf_update_theme
    command fzf "$@"
}

# Completion integration with dynamic theme refresh -------------------------------------------------------------------

_fzf_compgen_dir() {
    __fzf_update_theme
    fd --type d --hidden --follow --exclude ".git" . "$1"
}

_fzf_comprun() {
    local command=$1
    shift

    __fzf_update_theme

    case "$command" in
        cd)
            fzf "$@" --preview 'tree -C {} | head -200'
            ;;
        export|unset)
            fzf "$@" --preview "eval 'echo \$$'{}"
            ;;
        ssh)
            fzf "$@" --preview 'dig {}'
            ;;
        *)
            fzf "$@"
            ;;
    esac
}

