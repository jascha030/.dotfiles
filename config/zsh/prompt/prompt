#!/usr/bin/env zsh

# Prevent custom prompt from being sourced when running in a non-interactive shell.
if [[ ! -o login ]] || [[ ! -t 0 ]]; then 
    return;
fi

######################## Box Drawing Chars ################################
# U+250x	─	━	│	┃	┄	┅	┆	┇	┈	┉	┊	┋	┌	┍	┎	┏ #
# U+251x	┐	┑	┒	┓	└	┕	┖	┗	┘	┙	┚	┛	├	┝	┞	┟ #
# U+252x	┠	┡	┢	┣	┤	┥	┦	┧	┨	┩	┪	┫	┬	┭	┮	┯ #
# U+253x	┰	┱	┲	┳	┴	┵	┶	┷	┸	┹	┺	┻	┼	┽	┾	┿ #
# U+254x	╀	╁	╂	╃	╄	╅	╆	╇	╈	╉	╊	╋	╌	╍	╎	╏ #
# U+255x	═	║	╒	╓	╔	╕	╖	╗	╘	╙	╚	╛	╜	╝	╞	╟ #
# U+256x	╠	╡	╢	╣	╤	╥	╦	╧	╨	╩	╪	╫	╬	╭	╮	╯ #
# U+257x	╰	╱	╲	╳	╴	╵	╶	╷	╸	╹	╺	╻	╼	╽	╾	╿ #
###########################################################################

# if ((RANDOM % 2 == 0)); then
# export TERM_PRETTY="$(builtin echo -n ${TERM} | lolcrab)"
# | sed -e 's/\x1b\[39m\x1b\[49m//g')"
# else
# export TERM_PRETTY="$(builtin echo -n ${TERM} | lolcrab -i --sharp 1)"
# | sed -e 's/\x1b\[39m\x1b\[49m//g')"
# fi
# export TERM_PRETTY="$(builtin echo -n ${TERM})"
export TERM_PRETTY=${TERM_PRETTY:-$TERM}
source ${ZDOTDIR}/prompt/helpers.zsh

# Use https://github.com/romkatv/gitstatus/tree/master?tab=readme-ov-file for gitstatus if available
if [[ -f "${HOME}/tools/gitstatus/gitstatus.plugin.zsh" ]]; then
    source ${ZDOTDIR}/prompt/gitstatus.prompt.zsh
    export DOT_PROMPT_BETTER_GIT_STATUS=1
else
    autoload -Uz vcs_info
    precmd() { vcs_info }

    zstyle ':vcs_info:git:*' formats '|  %b'
    zstyle ':vcs_info:git:ignore' submodule 'all'
    zstyle ':vcs_info:*' enable hg git

    export DOT_PROMPT_BETTER_GIT_STATUS=0
fi

__fpath_add "$PROMPT_FUNCTIONS_PATH"
__autoload "$PROMPT_FUNCTIONS_PATH"
autoload -Uz add-zsh-hook

# In case of meer tab gekkigheid:
# https://unix.stackexchange.com/questions/14230/zsh-tab-completion-on-empty-line
function block-empty-tab {
    if [[ ${#BUFFER//[[:blank:]]/} != 0 ]]; then
        zle fzf-completion
    fi
    # zle expand-or-complete
    # zle expand-or-complete-prefix
}

# ── fill-line cache ────────────────────────────────────────────────────────────
# All fill-line inputs are static per $COLUMNS. Cache results and invalidate on resize.

typeset -gi __PROMPT_CACHED_COLS=0
typeset -g  __PROMPT_CACHE_TOP=''
typeset -g  __PROMPT_CACHE_MID=''
typeset -g  __PROMPT_CACHE_COMPACT=''

function __prompt_cache_invalidate {
    __PROMPT_CACHED_COLS=0
}

function __prompt_cache_rebuild {
    (( COLUMNS == __PROMPT_CACHED_COLS )) && return 0

    local REPLY

    # top-prompt
    local left="%F{012}    ${${SHELL/#\/bin\//}:u} |   ${TERM_PROGRAM} | %f${TERM_PRETTY}"
    local right='%F{011}   %w %b |   %T   %f'
    fill-line $left $right ' '
    __PROMPT_CACHE_TOP=$REPLY

    # mid-prompt
    fill-line $PS1_TL $PS1_TR
    __PROMPT_CACHE_MID=$REPLY

    # compact-prompt
    fill-line "  ╔" ""
    __PROMPT_CACHE_COMPACT=$REPLY

    __PROMPT_CACHED_COLS=$COLUMNS
}

function set-prompt {
    emulate -L zsh

    __prompt_cache_rebuild

    local php_wgt; php-ver

    local prompt_dir='%(5~|%-1~/…/%3~|%4~)'
    local git_prompt=
    if (( $DOT_PROMPT_BETTER_GIT_STATUS )); then
        git_prompt='${GITSTATUS_PROMPT}'
    else
        git_prompt='%F{08}${vcs_info_msg_0_}%f'
    fi

    local bot_left="${PS1_BL} %F{08} ${prompt_dir}%f ${git_prompt} "
    local bot_right="${php_wgt} ${PS1_BR}"

    PROMPT=$'\n'${__PROMPT_CACHE_TOP}$'\n'${__PROMPT_CACHE_MID}$'\n'${bot_left}
    RPROMPT=$bot_right
}

function set-compact-prompt {
    emulate -L zsh

    __prompt_cache_rebuild

    PROMPT="%F{00}${__PROMPT_CACHE_COMPACT}"$'\n'"  ╚   %f"
    RPROMPT=''
}

function zle-line-init {
    emulate -L zsh

    [[ $CONTEXT == start ]] || return 0

    while true; do
        zle .recursive-edit
        local -i ret=$?
        [[ $ret == 0 && $KEYS == $'\4' ]] || break
        [[ -o ignore_eof ]] || exit 0
    done

    local saved_prompt=$PROMPT
    local saved_rprompt=$RPROMPT

    set-compact-prompt
    zle .reset-prompt

    PROMPT=$saved_prompt
    RPROMPT=$saved_rprompt

    if (( ret )); then
        zle .send-break
    else
        zle .accept-line
    fi

    return ret
}

bottom-redisplay() {
    emulate -L zsh

    echoti clear
    position-prompt-bottom
    zle -R
}

zle -N bottom-redisplay

function TRAPWINCH () {
    emulate -L zsh

    __prompt_cache_invalidate
    set-prompt

    if [[ -o zle ]]; then
        zle -I
        zle bottom-redisplay
    fi
}

() {
    emulate -L zsh

    add-zsh-hook preexec position-prompt-bottom

    if (( DOT_PROMPT_BETTER_GIT_STATUS )); then
        add-zsh-hook precmd gitstatus_prompt_update
    fi

    zle -N zle-line-init
    zle -N block-empty-tab
    zle -N magic-enter
    zle -N bracketed-paste-force

    bindkey '^I' block-empty-tab
    bindkey "^M" magic-enter
    bindkey '^[[118;6u' bracketed-paste-force

    set-prompt

    if (( LOLMSGRC_ENABLED )); then
        clear-screen
    fi
}
